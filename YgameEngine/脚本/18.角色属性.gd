extends Node
class 职业与属性:
	# 存储加密的属性值
	var 属性表: Dictionary = {}  # {属性名: 加密值}
	var 属性点=引擎.加解密.浮点数加密(0.0)
	var 职业: String = ""           # 当前职业
	# 配置数据（模块本身不预设任何值，完全由外部定制）
	var 属性规则: Dictionary = {}  # 属性影响规则
	var 基础属性: Dictionary = {}   # 基础属性值,相当于初始属性
	
	# 构造函数
	func _init(初始职业: String = ""):
		职业 = 初始职业
	# ==============================================
	# 规则与基础属性配置
	# ==============================================
	
	# 配置属性规则
	# 规则格式: {属性名: {"通用": 数值, 职业名: 数值}
	func 配置属性规则(规则: Dictionary) -> void:
		属性规则 = 规则.duplicate(true)
	
	#  基础属性格式改为：{职业名: {属性名: 值}}
	func 配置基础属性(基础值: Dictionary) -> void:
		基础属性 = 基础值.duplicate(true)
	# 添加新属性（如果不存在）
	func 配置属性键(属性名称: String, 初始值: float = 0.0) -> void:
		if 属性名称 not in 属性表:
			属性表[属性名称] = 引擎.加解密.浮点数加密(初始值)
	
	# ==============================================
	# 属性管理
	# ==============================================
	
	
	# 设置属性值（自动加密）
	func 属性数值设置(属性名称: String, 值: float) -> bool:
		if 属性名称 not in 属性表:
			print("属性 "+属性名称+"不存在，请先添加属性")
			return false
		
		属性表[属性名称] = 引擎.加解密.浮点数加密(值)
		return true
	
	# 获取属性值（自动解密）
	func 属性数值获取(属性名称: String) -> float:
		if 属性名称 not in 属性表:
			return 0.0
		return 引擎.加解密.浮点数解密(属性表[属性名称])
	
	# 增加属性值
	func 属性数值增加(属性名称: String, 增加值: float,关联属性点:bool=true) -> bool:
		if 属性名称 not in 属性表:
			return false
			
		#如果需要属性点
		if 关联属性点:
			# 检查属性点是否足够
			if 属性点获取()<增加值:
				print("属性点不足，无法增加属性值")
				return false
			else:
				属性点减少(增加值)
		var 当前值 = 属性数值获取(属性名称)
		return 属性数值设置(属性名称, 当前值 + 增加值)
	
	# 
	func 属性表获取(解密:bool=true) -> Dictionary:
		if 解密:
			var 结果 = {}
			for 属性名 in 属性表:
				结果[属性名] = 属性数值获取(属性名)
			return 结果
		else:
			return 属性表
	# ==============================================
	# 属性点操作
	# ==============================================
	
	# 设置属性点（自动加密）
	func 属性点设置(值: float) -> void:
		属性点 = 引擎.加解密.浮点数加密(值)
	
	# 获取属性点（自动解密）
	func 属性点获取() -> float:
		return 引擎.加解密.浮点数解密(属性点)
	
	# 增加属性点
	func 属性点增加(增加值: float) -> void:
		var 当前值 = 属性点获取()
		属性点设置(当前值 + 增加值)
	
	# 减少属性点
	func 属性点减少(减少值: float) -> bool:
		var 当前值 = 属性点获取()
		if 当前值 >= 减少值:
			属性点设置(当前值 - 减少值)
			return true
		# 如果属性点不足，返回false
		return false
	# ==============================================
	# 职业
	# ==============================================
	
	
	# 设置职业
	func 职业设置(职业名: String) -> void:
		职业 = 职业名
	func 职业获取() -> String:
		return 职业
	
	# ==============================================
	# 属性计算
	# ==============================================
	class 计算结果:
		var 加密数据: Dictionary = {}
		var 加解密工具 = 引擎.加解密  # 引用加密工具
		
		# 构造函数：接收明文结果并加密
		func _init(明文结果: Dictionary):
			for 效果名 in 明文结果:
				加密数据[效果名] = 加解密工具.浮点数加密(明文结果[效果名])
		
		# 获取解密后的属性值
		func 获取唯一数据(数据名: String) -> float:
			if 数据名 not in 加密数据:
				return 0.0
			return 加解密工具.浮点数解密(加密数据[数据名])
			# 新增：获取所有属性（解密后返回字典）
		func 获取所有数据() -> Dictionary:
			var 所有属性 = {}
			for 效果名 in 加密数据:
				所有属性[效果名] = 加解密工具.浮点数解密(加密数据[效果名])
			return 所有属性
	# 计算属性效果
	func 计算属性效果(包含基础: bool = false) -> 计算结果:
		var 属性点 = 属性表获取()
		print(属性点)
		return 计算属性(属性点, 职业, 包含基础)
	
	# 计算最终属性的核心函数
	func 计算属性(属性点: Dictionary, 职业: String, 包含基础: bool = false) -> 计算结果:
		# 初始化结果字典
		var 最终属性 = {}
		if 包含基础:
			最终属性 = 基础属性.get(职业, {}).duplicate(true)
		else:
			# 从规则中收集所有可能的效果名并初始化为0
			for 属性名 in 属性规则:
					for 效果名 in 属性规则[属性名]:
						最终属性[效果名] = 0.0
		
		# 遍历每个属性点计算影响
		for 属性名 in 属性点:
			var 点数 = 属性点[属性名]
			if 属性名 not in 属性规则:
				continue  # 跳过无规则的属性
			
			var 效果字典 = 属性规则[属性名]
			for 效果名 in 效果字典:
				var 规则 = 效果字典[效果名]
				
				# 确保效果名存在于结果中
				if 效果名 not in 最终属性:
					最终属性[效果名] = 0.0
				
				# 计算通用加成
				最终属性[效果名] += 点数 * 规则.get("通用", 0.0)
				
				# 计算职业专属加成
				var 职业加成 =  规则.get(职业, 0.0)
				最终属性[效果名] += 点数 * 职业加成
		
		# 处理浮点数精度
		for 效果名 in 最终属性:
			最终属性[效果名] = round(最终属性[效果名] * 100) / 100
		
		# 返回临时结果对象（包含当前计算的加密数据）
		return 计算结果.new(最终属性)
