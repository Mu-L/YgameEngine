extends Node

class 生命魔法:
	# 存活状态标记
	var 是否存活: bool = true
	
	# 加密存储的核心属性（私有变量，通过方法操作）
	var _上限生命值 = 引擎.加解密.浮点数加密(10.0)
	var _上限魔法值 = 引擎.加解密.浮点数加密(5.0)
	var _当前生命值 = 引擎.加解密.浮点数加密(10.0)
	var _当前魔法值 = 引擎.加解密.浮点数加密(5.0)
	func 获取加密数据() -> Dictionary:
		var _加密数据 = {
			"上限生命值": _上限生命值,
			"上限魔法值": _上限魔法值,
			"当前生命值": _当前生命值,
			"当前魔法值": _当前魔法值
		}
   
		return _加密数据.duplicate()  # 返回副本，外部修改不影响内部数据
		
	func 获取是否存活() -> bool:
		return 是否存活
	# ==============================================
	# 上限HP/MP操作
	# ==============================================
	func 获取上限生命值() -> float:
		return 引擎.加解密.浮点数解密(_上限生命值)
	
	func 设置上限生命值(值: float,是否满血:bool=false) -> void:
		_上限生命值 = 引擎.加解密.浮点数加密(值)
		if 是否满血:
			设置当前生命值(值)
	
	func 获取上限魔法值() -> float:
		return 引擎.加解密.浮点数解密(_上限魔法值)
	
	func 设置上限魔法值(值: float,是否满魔:bool=false) -> void:
		_上限魔法值 = 引擎.加解密.浮点数加密(值)
		if 是否满魔:
			设置当前魔法值(值)


	# ==============================================
	# 当前HP/MP操作
	# ==============================================
	func 获取当前生命值() -> float:
		return 引擎.加解密.浮点数解密(_当前生命值)
	
	func 设置当前生命值(值: float) -> void:
		# 限制在0到上限之间
		var 有效生命值 = clamp(float(值), 0.0, 获取上限生命值())
		_当前生命值 = 引擎.加解密.浮点数加密(有效生命值)
		# 同步更新存活状态
		是否存活 = 有效生命值 > 0.0
	
	func 获取当前魔法值() -> float:
		return 引擎.加解密.浮点数解密(_当前魔法值)
	
	func 设置当前魔法值(值: float) -> void:
		# 限制在0到上限之间
		var 有效魔法值 = clamp(float(值), 0.0, 获取上限魔法值())
		_当前魔法值 = 引擎.加解密.浮点数加密(有效魔法值)


	# ==============================================
	# 战斗快捷操作（增减值封装）
	# ==============================================
	func 生命值恢复(恢复值: float) -> void:
		设置当前生命值(获取当前生命值() + 恢复值)
	
	func 生命值消耗(消耗值: float) -> void:
		设置当前生命值(获取当前生命值() - 消耗值)
	
	func 魔法值恢复(恢复值: float) -> void:
		设置当前魔法值(获取当前魔法值() + 恢复值)
	
	func 魔法值消耗(消耗值: float) -> void:
		设置当前魔法值(获取当前魔法值() - 消耗值)
